//#
//#  COMPONENT_NAME: somi
//#
//#  ORIGINS: 27
//#
//#
//#    25H7912  (C)   COPYRIGHT International Business Machines Corp. 1992,1994,1996,1996  
//#  All Rights Reserved
//#  Licensed Materials - Property of IBM
//#  US Government Users Restricted Rights - Use, duplication or
//#  disclosure restricted by GSA ADP Schedule Contract with IBM Corp.
//#
//#  @(#) 2.9.1.4 src/somi/ir/somir/attribdf.idl, somi.corba, som2.1 1/3/96 14:28:13 [7/30/96 14:46:18]


//# AttributeDef: CORBA Interface for attributed definitions
//# See CORBA 1.1, 7.5.5, p.135

#ifndef attribdf_idl
#define attribdf_idl

#include <containd.idl>
#include <somcls.idl>

interface AttributeDef : Contained
// This interface is used to access information associated with
// attributes defined in an IDL interface.
//
// See CORBA 1.1, 7.5.5, p.135
{
    enum AttributeMode {NORMAL, READONLY};

    struct AttributeDescription {
	Identifier    name;
	RepositoryId  id;
	RepositoryId  defined_in;
	TypeCode      type;
	AttributeMode mode;
    };
    // The inherited describe method returns an instance of this
    // (AttributeDescription) structure in the "value" member of the
    // Description structure defined in the Contained interface.

    attribute TypeCode type;
    // The TypeCode of the receiving object.  The memory used to hold
    // the TypeCode is contained in the receiving object, which retains
    // ownership.  Hence, do not free the returned TypeCode.  If you want
    // to obtain a separate copy, use the TypeCode_copy operation.

    attribute AttributeMode mode;
    // The AttributeMode of the receiving object;

#ifdef __SOMIDL__
  implementation {
      releaseorder: _get_type,_set_type,_get_mode,_set_mode
      ;

      //# Class Modifiers
      callstyle = idl;
      majorversion = 2;
      minorversion = 3;
      filestem = attribdf;
      dllname = "somir.dll";

      passthru C_h =
	""
	"    #include <somtcnst.h>"
	"    #include <somir.h>"
	"    #include <containd.h>"
	"";
      passthru C_xh =
	""
	"    #include <somtcnst.xh>"
	"    #include <somir.xh>"
	"    #include <containd.xh>"
	"";

      //# Attribute Modifiers
      type: nodata;
      mode: noget, noset;
#if defined __SOM_KERNEL_LEVEL && __SOM_KERNEL_LEVEL >= 250
      _get_type : dual_owned_result;
#else
      _set_type : caller_owns_parameters = "type";
#endif

      //# Method Modifiers
      somDefaultInit: override, init;
      somDestruct: override;
      somDumpSelf: override;
      somDumpSelfInt: override;
      describe: override;
  };
#endif /* __SOMIDL__ */
};

#endif	/* attribdf_idl */
